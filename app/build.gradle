apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'io.objectbox'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "knez.assdroid"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 13
        versionName "1.0.0-knez"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
        buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [appName: "knez.assdroid.App"]

            buildConfigField "boolean", "DEBUG_LOG_CURRENT_THREAD", "false"
            buildConfigField "boolean", "DEBUG_LOG_FRAGMENT_STATE", "false"
            buildConfigField "boolean", "STRICT_MODE", "false"
        }
        debug {
            manifestPlaceholders = [appName: "knez.assdroid.DebugApp"]

            buildConfigField "boolean", "DEBUG_LOG_CURRENT_THREAD", "true"
            buildConfigField "boolean", "DEBUG_LOG_FRAGMENT_STATE", "true"
            buildConfigField "boolean", "STRICT_MODE", "true"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.annotation:annotation:1.0.0'

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton:butterknife:9.0.0-rc1'
    implementation 'info.android15.solid:collections:3.0.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'io.gsonfire:gson-fire:1.8.2'
    implementation "com.mikepenz:iconics-core:3.1.0"
    implementation "com.mikepenz:iconics-views:3.1.0@aar"
    implementation 'com.mikepenz:google-material-typeface:3.0.1.3.original@aar'
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'

    implementation "com.mikepenz:materialdrawer:6.1.1"

    implementation "com.mikepenz:aboutlibraries:6.2.0"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'

    implementation 'com.jakewharton:process-phoenix:2.0.0'

    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-rc1'
    implementation 'com.google.dagger:dagger:2.17'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.17'
    implementation 'com.google.dagger:dagger-android:2.17'
    implementation 'com.google.dagger:dagger-android-support:2.17'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.17'
    androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.17'
    androidTestAnnotationProcessor 'com.google.dagger:dagger-android-processor:2.17'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.3@aar') {
        transitive = true
    }
}


def gitSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

def gitTimestamp() {
    def p = 'git log -n 1 --format=%at'.execute([], rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}